buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayGradleVersion}"
    }
}


apply plugin: 'kotlin'
apply plugin: "jacoco"
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}


jacoco {
    toolVersion = "${jacocoToolVersion}"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

check.dependsOn jacocoTestReport

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile "commons-io:commons-io:${commonsIOVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testCompile "ch.qos.logback:logback-classic:${logbackVersion}"
    testCompile "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
    testCompile "org.assertj:assertj-core:${assertJVersion}"
    testCompile "junit:junit:${junitVersion}"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.0"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        userOrg = 'kotlins'
        repo = 'org.kotlins'
        name = 'funpdf'
        desc = "Wkhtmltopdf Wrapper for Kotlin."
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kotlins/funpdf.git'
        issueTrackerUrl = 'https://github.com/kotlins/funpdf/issues'
        labels = ['kotlin', 'pdf', 'wkthmltopdf']
        publicDownloadNumbers = true
        override = true
        version {
            name = "${project.version}"
            desc = "funpdf version ${project.version}, Need More Test!."
            vcsTag = "${project.version}"
        }
    }
}